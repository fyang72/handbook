

$PROB LN002 model with linear elimination (concentration only)


$INPUT   C=DROP ROWID	USUBJID=DROP	SUBJECTN	ID	TRTGROUP=DROP	ARMA=DROP	ARMAN	VISIT
	VISITNUM	TIMEPT=DROP	NTIM	TIME	TIME_FLAG	TAD	OBS	DV	STDUNIT=DROP	BLQ=DROP
  	MDV	TEST=DROP	CMT	EXROUTN	EXDOSE	EXDOSU=DROP	EXTDOSE	EXTDOSU=DROP	
    EXDURAT=DROP	EXSEQ	AMT	RATE	AMTMKG	AMTTOT	RATEMKG	RATETOT	EVID	
    AGE	SEX=DROP	SEXN	RACE=DROP	RACEN	ETHNIC=DROP	ETHNICN	
    WEIGHTBL	HEIGHTBL	BMIBL ALB CRT ESP ALT AST  
 
$DATA  ../../data/nmdat4LN002.csv  IGNORE=C     
       ;IGNORE=@
       ;IGNORE=(FLAG.NE.0) 
       ;IGNORE=(NOPK.EQ.1)

$SUBROUTINE ADVAN4 TRANS4
 

$PK 
 IF (NEWIND.EQ.0) THEN
   LLOQ1 = 0.078 ; MG/L
 ENDIF

 TVCL = THETA(1)           ; POPULATION CLEARANCE (L/DAY)
 TVQ  = THETA(2)           ; POPULATION INTERCOMPARTMENTAL CLEARANCE (L/DAY)
 TVV2 = THETA(3)           ; POPULATION CENTRAL VOLUME (L) 
 TVV3 = THETA(4)           ; POPULATION PERIPHERAL COMPARTMENT VOLUME (L) 
 LF1  = THETA(5)           ; POPULATION BIOAVAIBILITY 
 TVKA = THETA(6)           ; POPULATION ABSORPTION RATE CONSTANT (1/DAY) 
 RUVCV = THETA(7)
 RUVSD = THETA(8)
 
 ; COVARIATE MODEL
 WT_ON_CLQ = THETA(9)
 WT_ON_VSS = THETA(10)

 M_WT = 75 ; kg 
 COVCL = WT_ON_CLQ *(LOG(WEIGHTBL)-LOG(M_WT))
 COVQ  = WT_ON_CLQ *(LOG(WEIGHTBL)-LOG(M_WT))

 COVV2 = WT_ON_VSS *(LOG(WEIGHTBL)-LOG(M_WT))
 COVV3 = WT_ON_VSS *(LOG(WEIGHTBL)-LOG(M_WT))

 CL = TVCL*EXP(ETA(1)+COVCL)          ; CL, INDIVIDUAL CLEARANCE (L/DAY)
 Q  = TVQ *EXP(ETA(1)+COVQ)           ; Q, INDIVIDUAL INTERCOMPARTMENTAL CLEARANCE (L/DAY)

 V2 = TVV2*EXP(ETA(2)+COVV2)          ; V2, INDIVIDUAL CENTRAL VOLUME (L) 
 V3 = TVV3*EXP(ETA(2)+COVV3)          ; V3, INDIVIDUAL PERIPHERAL VOLUME (L) 

 KA = TVKA*EXP(ETA(3))                ; KA, INDIVIDUAL ABSORPTION RATE CONSTANT (1/DAY) 
 

 ;DERIVED PARAMETERS
 K = CL/V2
 K23 = Q/V2
 K32 = Q/V3

 S2 = V2     ; dose = mg, conc = mg/L
 F1 = LF1    ; Or EXP(LF1)/(1+EXP(LF1))
 
;CALCULATE TIME AFTER DOSE
 IF(NEWIND.NE.2) TDOS=0
 IF(AMT.GT.0) TDOS=TIME
 ;TAD=TIME-TDOS         ; TIME RELATIVE TO DOSE
                                              
 

$ERROR (OBSERVATION ONLY) 
 CALLFL = 0

 EPS0 = 1E-5
 CONCI = A(2)/S2
 IF (CONCI.LE.EPS0) CONCI = EPS0
 IPRED = LOG(CONCI)

 PROP=EXP(RUVCV)*IPRED ; proportional part 
 ADD=EXP(RUVSD) ; additive part 
 SD=SQRT(PROP*PROP + ADD*ADD) ; Standard deviation using proportional residual error 
     
 BLOQ1 = 0
 IF (BLOQ1.EQ.0) THEN
   F_FLAG = 0
   Y = IPRED + SD*ERR(1)
 ELSE
   F_FLAG = 1 
   Y = PHI((LOG(LLOQ1)-LOG(CONCI))/SD)
 ENDIF
 
 IRES  = DV - IPRED
 IWRES = IRES/SD

$THETA  
(0,0.15)  ;TVCL
(0,0.45)  ;TVQ
(0,3.00)  ;TVV2
(0,1.50)  ;TVV3
(0,0.70)  ;TVF1
(0,0.40)  ;TVKA
(-10)     ;RUVCV
(-1)      ;RUVSD
(0.75)    ;WT_ON_CLQ
(1.0)     ;WT_ON_VSS


$OMEGA  BLOCK(2)
 0.1341               ; IIV_CLQ
 0.0159127 0.0417008  ; IIV_VSS

  
$OMEGA
 0.01     ;OMEGA-4 IIV_KA

 
$SIGMA
1 FIXED  ;SIGMA-1
 

$EST MAXEVAL=9999 NSIG=3 SIGL=9 METHOD=1 PRINT=10 NOABORT POSTHOC MSFO=001.MSF NOTHETABOUNDTEST NOOMEGABOUNDTEST NOSIGMABOUNDTEST  
 
$COV PRINT=E UNCONDITIONAL MATRIX=S SIGL=12

;$SIMULATION (20102) (330333 UNIFORM) ONLYSIM SUBPROBLEMS=1


;$EST   METHOD=ITS INTERACTION LAPLACIAN NITER=100 NSIG=3 SIGL=9 CTYPE=2 CINTERVAL=50 NOABORT PRINT=5  GRD=TS(7) NOCOV=1 FILE=blq.ext
;$EST   METHOD=SAEM INTERACTION LAPLACIAN NBURN=10000 NITER=1000 CTYPE=2 PRINT=50 NOCOV=1   CINTERVAL=50 ISAMPLE=2 GRD=TS(7) FILE=blq_saem.ext
;$EST   METHOD=IMP INTERACTION LAPLACIAN ISAMPLE=2000 NITER=10 EONLY=1 PRINT=1 MAPITER=0 NOCOV=0       GRD=TS(7) DF=0    
;$COV   MATRIX=R COMPRESS UNCOND PRINT=E


;************************************************************
;**** TABLES ****
;************************************************************ 
;The 'standard' parameters, including IWRE, IPRE, TIME, and the NONMEM default items (DV, PRED, RES and WRES)
$TABLE ROWID ID TIME TAD IPRED PRED DV MDV IWRES CWRES RES WRES ETA1 ETA2 ETA3  NOAPPEND NOPRINT ONEHEADER  FILE=sdtab001  

;The empirical Bayes estimates of individual model parameter values, or posthoc estimates.  
$TABLE ID  CL  Q  V2  V3 F1 KA RUVCV RUVSD WT_ON_CLQ WT_ON_VSS NOPRINT ONEHEADER FILE=patab001  

;catab5: Categorical covariates, e.g. SEX, RACE.
;$TABLE ID ARMAN SEXN RACEN ETHNICN     NOPRINT NOAPPEND ONEHEADER FILE=catab001        

;cotab5: Continuous covariates, e.g. WT, AGE.
;$TABLE ID  AGE  WEIGHTBL HEIGHTBL BMIBL NOPRINT NOAPPEND ONEHEADER FILE=cotab001
 
;mutab5, mytab5, extra5, xptab5: Additional variables of any kind. These might be useful 
;if there are more covariates than can be accommodated in the covariates tables, for example, 
;or if you have other variables that should be added, e.g. CMAX, AUC. Value
  
 
